plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
}

def propertiesFile = rootProject.file('local.properties')
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

def androidVersions = rootProject.ext.projectConfiguration

android {
    compileSdk androidVersions.compileSdk

    defaultConfig {
        applicationId androidVersions.applicationId
        minSdk androidVersions.minSdk
        targetSdk androidVersions.targetSdk
        versionCode androidVersions.versionCode
        versionName androidVersions.versionName

        /*
         * If your minSdkVersion is set to 21 or higher, multidex is enabled by default
         * and you do not need the multidex support library.
         */
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        /**
         * User like this in manifest
         *
         * <meta-data
         *      android:name="com.google.android.geo.API_KEY"
         *      android:value="${GOOGLE_MAPS_API_KEY}" />
         */
        manifestPlaceholders = [GOOGLE_MAPS_API_KEY: "${properties.getProperty('GOOGLE_MAPS_API_KEY')}"]
        /**
         * User like this in manifest
         *
         * <meta-data
         *      android:name="com.google.android.geo.API_KEY"
         *      android:value="@string/google_maps_api_key" />
         */
        resValue "string", "google_maps_api_key", "${properties.getProperty('GOOGLE_MAPS_API_KEY')}"
        /**
         * User like this in java
         *
         * public static final String GOOGLE_MAP_API_KEY = BuildConfig.GOOGLE_MAP_API_KEY;
         */
        buildConfigField "String", "GOOGLE_MAPS_API_KEY", "\"${properties.getProperty('GOOGLE_MAPS_API_KEY')}\""

        manifestPlaceholders = [FIREBASE_SERVER_KEY: "${properties.getProperty('FIREBASE_SERVER_KEY')}"]
        resValue "string", "firebase_server_key", "${properties.getProperty('FIREBASE_SERVER_KEY')}"
        buildConfigField "String", "FIREBASE_SERVER_KEY", "\"${properties.getProperty('FIREBASE_SERVER_KEY')}\""
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
        dataBinding = false
    }

    /** Enable code shrinking, obfuscation, and optimization for only release build types else it
     * increases the build time and makes it difficult to debug.
     */
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    ndkVersion '23.1.7779620'
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk' //path of Android.mk file
        }
    }
}

dependencies {
    implementation project(':library-pinview')
   implementation project(':library-fab-bottomnavigationview')
    implementation project(':library-utilities')
    implementation project(':library-networkconnectivity')
    implementation project(':library-sharedpreferences')
    implementation project(':library-adapter')
    implementation project(':library-sliderview')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    /**
     * However, if your minSdkVersion is set to 20 or lower,
     * then you must use the multiDex support library
     */
    implementation "androidx.multidex:multidex:2.0.1"

    /* Google Material Design Libraries */
    implementation "com.google.android.material:material:1.6.1"

    /* use this two library, ssp is for text size, and sdp is for margin, padding and layout size */
    implementation 'com.intuit.ssp:ssp-android:1.0.6'
    implementation 'com.intuit.sdp:sdp-android:1.0.6'

    /* View Libraries */
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"

    /********************************** JetPack Libraries Start ***********************************/
    // Navigation
    implementation 'androidx.navigation:navigation-fragment:2.5.2'
    implementation 'androidx.navigation:navigation-ui:2.5.2'

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:2.38.1"
    annotationProcessor "com.google.dagger:hilt-compiler:2.38.1"

    // Lifecycle only (without ViewModel or LiveData) for Java & Kotlin
    implementation "androidx.lifecycle:lifecycle-runtime:2.5.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    // ViewModel for Java & Kotlin
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"

    // LiveData for Java & Kotlin
    implementation "androidx.lifecycle:lifecycle-livedata:2.5.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    /*********************************** JetPack Libraries End ************************************/
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    /* Retrofit & OkHttp Libraries */
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    /* Glide Libraries */
    implementation "com.github.bumptech.glide:glide:4.13.1"
    annotationProcessor "com.github.bumptech.glide:compiler:4.13.0"

    /* Google Libraries */
    implementation "com.google.code.gson:gson:2.9.0"

    /**
     * Logging Interceptor
     * link : https://github.com/ihsanbal/LoggingInterceptor
     */
    implementation('com.github.ihsanbal:LoggingInterceptor:3.1.0') {
        exclude group: 'org.json', module: 'json'
    }

    /**
     * Circle Indicator
     * link : https://github.com/ongakuer/CircleIndicator
     */
    implementation 'me.relex:circleindicator:2.1.6'
}
